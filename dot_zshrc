# ==========================
# PATH and Environment Setup
# ==========================
export PATH="/opt/homebrew/bin:/opt/homebrew/opt:${HOME}/.local/bin:/usr/local/bin/tailscale:$HOME/go/bin:$HOME/.bun/bin:$PATH"

# Cache brew prefix to avoid multiple calls
BREW_PREFIX="/opt/homebrew"

fpath+=("$BREW_PREFIX/share/zsh/site-functions")
FPATH=~/.zfunc:$FPATH

# Set XDG config directory
export XDG_CONFIG_HOME="$HOME/.config"

# Set color schemes for various tools
# Cache vivid output to avoid generating twice
_VIVID_COLORS="$(vivid generate catppuccin-mocha)"
export LS_COLORS="$_VIVID_COLORS"
export EZA_COLORS="$_VIVID_COLORS"
export VIRTUAL_ENV_DISABLE_PROMPT=true

# ==========================
# FZF
# ==========================
# Catppuccin theme
FZF_DEFAULT_OPTS=" \
--color=bg+:#313244,bg:#1e1e2e,spinner:#f5e0dc,hl:#f38ba8 \
--color=fg:#cdd6f4,header:#f38ba8,info:#cba6f7,pointer:#f5e0dc \
--color=marker:#b4befe,fg+:#cdd6f4,prompt:#cba6f7,hl+:#f38ba8 \
--color=selected-bg:#45475a \
--multi"

export FZF_DEFAULT_COMMAND="fd --hidden --strip-cwd-prefix --exclude .git"
export FZF_CTRL_T_COMMAND="$FZF_DEFAULT_COMMAND"
export FZF_ALT_C_COMMAND="fd --type=d --hidden --strip-cwd-prefix --exclude .git"

# Preview command for files and directories
show_file_or_dir_preview="if [ -d {} ]; then eza --tree --color=always {} | head -200; else bat --color=always --line-range :500 {}; fi"
export FZF_CTRL_T_OPTS="--preview '$show_file_or_dir_preview'"
export FZF_ALT_C_OPTS="--preview 'eza --tree --color=always {} | head -200'"

# ==========================
# Prompt Initialization
# ==========================
# Init Starship - prompt
eval "$(starship init zsh)"

# ==========================
# Plugin Initialization
# ==========================
# Initialize Zsh completion
# Only regenerate compdump once per day for faster startup
autoload -Uz compinit
# Check if dump is older than 24 hours
if [[ -n ~/.zcompdump(#qNmh-24) ]]; then
  # Dump is fresh, skip check (-C flag)
  compinit -C
else
  # Dump is old or missing, regenerate
  compinit
fi

# Check if Homebrew is installed
if type brew &>/dev/null; then
  # Add Homebrew completions to FPATH
  FPATH=$BREW_PREFIX/share/zsh-completions:$FPATH
  
  # Source Zsh plugins
  source ~/.zsh_plugins/fzf-tab/fzf-tab.plugin.zsh
  # source "$BREW_PREFIX/share/zsh-autosuggestions/zsh-autosuggestions.zsh"
  
  # Defer syntax highlighting to improve startup time (saves ~6ms)
  _zsh_highlight_deferred_init() {
    source "$BREW_PREFIX/share/zsh-syntax-highlighting/zsh-syntax-highlighting.zsh"
    precmd_functions=(${precmd_functions:#_zsh_highlight_deferred_init})
  }
  precmd_functions+=(_zsh_highlight_deferred_init)
  
  source "$BREW_PREFIX/opt/zsh-vi-mode/share/zsh-vi-mode/zsh-vi-mode.plugin.zsh"
  
  # Cache fzf integration to avoid process substitution on every shell start
  _FZF_ZSH_CACHE="$HOME/.cache/fzf-zsh-integration"
  if [[ ! -f "$_FZF_ZSH_CACHE" ]] || [[ "$(command -v fzf)" -nt "$_FZF_ZSH_CACHE" ]]; then
    fzf --zsh > "$_FZF_ZSH_CACHE"
  fi
  source "$_FZF_ZSH_CACHE"
  
fi

# ---- Carapace -----
if command -v carapace &> /dev/null; then
  export CARAPACE_BRIDGES='zsh,fish,bash,inshellisense'
  zstyle ':completion:*' format $'\e[2;37mCompleting %d\e[m'
  
  # Cache carapace completion to avoid process substitution
  _CARAPACE_CACHE="$HOME/.cache/carapace-zsh-completion"
  if [[ ! -f "$_CARAPACE_CACHE" ]] || [[ "$(command -v carapace)" -nt "$_CARAPACE_CACHE" ]]; then
    carapace _carapace > "$_CARAPACE_CACHE"
  fi
  source "$_CARAPACE_CACHE"
fi

# ---- Completion settings -----
# Set up case-insensitive matching for completion.
# This allows the completion system to match lowercase and uppercase letters interchangeably.
zstyle ':completion:*' matcher-list 'm:{a-z}={A-Za-z}'
# Set the colors used in the completion list to match the LS_COLORS environment variable.
# This ensures that the colors used in the completion output are consistent with those used in the terminal.
zstyle ':completion:*' list-colors "${(s.:.)LS_COLORS}"
# Disable the menu selection for completion.
# This means that when you press Tab, it will not show a menu of completions; instead, it will directly complete the command.
zstyle ':completion:*' menu no
# To make fzf-tab follow FZF_DEFAULT_OPTS.
# NOTE: This may lead to unexpected behavior since some flags break this plugin. See Aloxaf/fzf-tab#455.
zstyle ':fzf-tab:*' use-fzf-default-opts yes

# ---- Zoxide -----
eval "$(zoxide init --cmd cd zsh)"

# ==========================
# Atuin Initialization
# ==========================
zvm_after_init_commands+=(eval "$(atuin init zsh --disable-up-arrow)")

# ==========================
# Aliases
# ==========================

# ---- Editor Aliases ----
alias vim="nvim"  # Use nvim as vim
alias vi="nvim"  # Use nvim as vi
alias vif="cdi && nvim ."  # Find directories and edit with Neovim
alias viff="fzf --preview 'bat --color=always {}' | xargs nvim"  # Find files and edit with Neovim
alias codef="cdi && code ."  # Find directories and edit with VSCode
alias codeff="fzf --preview 'bat --color=always {}' | xargs code"  # Find files and edit with VSCode
alias cf="codef"
alias cff="codeff"

# ---- Navigation Aliases ----
alias cdf="cdi"
alias ".."="cd .."  # Go up one directory
alias "..."="cd ../.."  # Go up two directories
alias "...."="cd ../../.."  # Go up three directories
alias work="cd ~/Workspace"  # Go to Workspace directory

# ---- System & Tools Aliases ----
alias ls="eza --long --group-directories-first --icons"  # Use eza for ls
alias cat="bat"  # Use bat instead of cat
alias mr="mise run"  # Alias for mise run
alias cm="chezmoi"  # Alias for chezmoi
alias venva="source .venv/bin/activate"  # Activate venvs
alias reload="cm re-add ~/.config/nvim/lazy-lock.json && cm status && cm apply && source ~/.zshrc"
alias brewup="brew update && brew upgrade && brew upgrade --cask --greedy && brew cleanup && brew autoremove"
alias ld="lumen diff --theme catppuccin-mocha"
alias ldw="lumen diff --theme catppuccin-mocha --watch"

# ---- Git Aliases ----
alias gs="git status"  # Show working tree status
alias ga="git add"  # Add files to staging
alias gc="git commit -m"  # Commit with message
alias gp="git push"  # Push to remote
alias gpl="git pull"  # Pull from remote
alias gd="git diff"  # Show changes
alias gl="git log --oneline --graph --decorate"  # Show commit history
alias gco="git checkout"  # Switch branches
alias gb="git branch"  # List branches

alias lg="lazygit"  # Launch lazygit
alias lgf="cdi && lazygit"  # Find directories and launch lazygit

# ---- Terraform Aliases ----
alias tf="terraform"

# ---- OpenCode Aliases ----
alias oc="opencode"  # OpenCode CLI
alias occ="opencode --continue"  # Continue last session
alias ocf="cdi && opencode"  # Find directories and open with OpenCode

# ---- Typora ------
alias md="open -a Typora"
alias mdf="fzf --preview 'bat --color=always {}' | xargs open -a Typora"

# ==========================
# Environment Variables
# ==========================
export AWS_REGION="eu-west-1"  # Set AWS region
export AWS_DEFAULT_REGION="eu-west-1"  # Set default AWS region
export AWS_PROFILE="oddin"  # Set AWS profile
export EDITOR="nvim"  # Set default editor

# ==========================
# Mise Initialization
# ==========================
# Defer mise activation to speed up shell startup (saves ~20ms)
# Load in background using precmd hook - mise will be ready before you cd anywhere
_mise_deferred_init() {
  eval "$($BREW_PREFIX/bin/mise activate zsh)"
  # Remove this function from precmd after first run
  precmd_functions=(${precmd_functions:#_mise_deferred_init})
}
precmd_functions+=(_mise_deferred_init)

# ==========================
# Custom Functions
# ==========================
# yazi settings to enable exit and stay in changed directory
function y() {
	local tmp="$(mktemp -t "yazi-cwd.XXXXXX")" cwd
	yazi "$@" --cwd-file="$tmp"  # Run yazi with cwd file
	if cwd="$(command cat -- "$tmp")" && [ -n "$cwd" ] && [ "$cwd" != "$PWD" ]; then
		builtin cd -- "$cwd"  # Change directory if cwd is different
	fi
	rm -f -- "$tmp"  # Clean up temporary file
}

# Use fd (https://github.com/sharkdp/fd) for listing path candidates.
# - The first argument to the function ($1) is the base path to start traversal
# - See the source code (completion.{bash,zsh}) for the details.
_fzf_compgen_path() {
  fd --hidden --exclude .git . "$1"
}

# Use fd to generate the list for directory completion
_fzf_compgen_dir() {
  fd --type=d --hidden --exclude .git . "$1"
}

# Advanced customization of fzf options via _fzf_comprun function
# - The first argument to the function is the name of the command.
# - You should make sure to pass the rest of the arguments to fzf.
_fzf_comprun() {
  local command=$1
  shift

  case "$command" in
    cd)           fzf --preview 'eza --tree --color=always {} | head -200' "$@" ;;
    export|unset) fzf --preview "eval 'echo \${}'"         "$@" ;;
    ssh)          fzf --preview 'dig {}'                   "$@" ;;
    *)            fzf --preview "$show_file_or_dir_preview" "$@" ;;
  esac
}

# Open last markdown file in .opencode/plans with Typora
function mdp() {
  local plans_dir=".opencode/plans"
  if [ ! -d "$plans_dir" ]; then
    echo "Error: $plans_dir directory not found"
    return 1
  fi
  
  local last_plan=$(find "$plans_dir" -maxdepth 1 -name "*.md" -type f -print0 | xargs -0 ls -t 2>/dev/null | head -1)
  if [ -z "$last_plan" ]; then
    echo "Error: No markdown files found in $plans_dir"
    return 1
  fi
  
  open -a Typora "$last_plan"
}
